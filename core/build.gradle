apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    buildToolsVersion = versions.buildTools
    compileSdkVersion versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    flavorDimensions "features"
    productFlavors {
        core {
            versionCode 2602
            versionName '0.26.2'
        }
        plugins {
            versionCode 500
            versionName "0.5.0"
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "gast-${variant.getFlavorName()}.${variant.mergedFlavor.versionName}.${variant.buildType.name}.aar"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Common dependencies
    compileOnly(fileTree(dir: "../libs", include: ["godot-lib*.aar"]))

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlinVersion}"
    implementation("androidx.legacy:legacy-support-v4:1.0.0")

    // Dependencies for the `plugins` flavor
    pluginsImplementation("com.google.android.exoplayer:exoplayer:2.14.1")
}

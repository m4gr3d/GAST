apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply from: "../scripts/publish-module.gradle"

android {
    buildToolsVersion = versions.buildTools
    compileSdkVersion versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    flavorDimensions "features"
    productFlavors {
        core {
            versionCode versions.coreVersionCode
            versionName versions.coreVersionName
        }
        plugins {
            versionCode versions.pluginsVersionCode
            versionName versions.pluginsVersionName

            manifestPlaceholders = [pluginsVersion: versionName]
        }
        xrapp {
            versionCode versions.xrappVersionCode
            versionName versions.xrappVersionName

            manifestPlaceholders = [xrAppVersion: versionName]
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "gast-${variant.getFlavorName()}.${variant.mergedFlavor.versionName}.${variant.buildType.name}.aar"
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }
    kotlinOptions {
        jvmTarget = versions.kotlinJvmTarget
    }

    // TODO: Enable when issues with AGP 7.1+ are resolved (https://github.com/GodotVR/godot_openxr/issues/187).
//    publishing {
//        singleVariant("coreRelease") {
//            withSourcesJar()
//            withJavadocJar()
//        }

    //        singleVariant("xrappRelease") {
//            withSourcesJar()
//            withJavadocJar()
//        }
//    }
}

dependencies {
    // Common dependencies
    implementation "io.github.m4gr3d:godot:${versions.godotVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlinVersion}"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"

    // Dependencies for the `plugins` flavor
    pluginsImplementation("com.google.android.exoplayer:exoplayer:2.15.1")

    // Dependencies for the 'xrapp' flavor
    xrappImplementation "io.github.m4gr3d:godot-openxr:${versions.godotOpenXRVersion}"

    // ViewModel
    xrappImplementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"

    xrappImplementation "androidx.appcompat:appcompat:1.3.1"
    xrappImplementation "androidx.activity:activity-ktx:1.3.1"
    xrappImplementation "androidx.fragment:fragment-ktx:1.3.1"
}

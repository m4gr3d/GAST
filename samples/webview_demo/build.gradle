plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    buildToolsVersion = versions.buildTools
    compileSdkVersion versions.compileSdk
    ndkVersion versions.ndkVersion

    defaultConfig {
        // The default ignore pattern for the 'assets' directory includes hidden files and directories which are used by Godot projects.
        // We also ignore the 'android' build directory which is generated by the Godot editor.
        aaptOptions {
            ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~:<dir>android"
        }

        applicationId "org.godotengine.plugin.gast.samples.webview"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        missingDimensionStrategy 'features', 'plugins'
    }

    sourceSets {
        main {
            assets.srcDirs += ['godot/']
        }
    }

    flavorDimensions "xrMode"
    productFlavors {
        ovr {
            applicationIdSuffix ".ovr"
            versionNameSuffix "-ovr"
        }
        pancake {
            applicationIdSuffix ".pancake"
            versionNameSuffix "-pancake"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Common dependencies
    implementation project(path: ':core')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlinVersion}"
    implementation "androidx.multidex:multidex:$versions.multidex_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "io.github.m4gr3d:godot:${versions.godotVersion}"

    // Dependencies for the `ovr` flavor
    ovrImplementation(fileTree(dir: "../libs", include: ["gdovrmobile*.aar"]))
}

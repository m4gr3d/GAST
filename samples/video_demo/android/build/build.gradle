// Gradle build config for Godot Engine's Android port.
apply from: 'config.gradle'

buildscript {
    apply from: 'config.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath libraries.androidGradlePlugin
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlinVersion")
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

dependencies {
    if (rootProject.findProject(":lib")) {
        implementation project(":lib")
    } else if (rootProject.findProject(":godot:lib")) {
		implementation project(":godot:lib")
	} else {
        // Custom build mode. In this scenario this project is the only one around and the Godot
        // library is available through the pre-generated godot-lib.*.aar android archive files.
        debugImplementation fileTree(dir: 'libs/debug', include: ['*.jar', '*.aar'])
        releaseImplementation fileTree(dir: 'libs/release', include: ['*.jar', '*.aar'])
    }

	// Godot plugins dependencies
	implementation fileTree(dir : 'libs/plugins', include : getGodotPluginsBinaries())

    implementation("com.android.support:support-v4:28.0.0")
    implementation project(":gast-lib")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlinVersion")
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        // Feel free to modify the application id to your own.
        applicationId getExportPackageName()
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'UnusedResources'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    // Both signing and zip-aligning will be done at export time
    /*buildTypes.all { buildType ->
        buildType.zipAlignEnabled false
        buildType.signingConfig null
    }*/

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            aidl.srcDirs = ['aidl']
            assets.srcDirs = ['assets']
        }
        debug.jniLibs.srcDirs = ['libs/debug']
        release.jniLibs.srcDirs = ['libs/release']
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "android_${variant.name}.apk"
        }
    }
}
